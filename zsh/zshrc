# use nvim if available, fallback to vim
export EDITOR=$(which nvim &>/dev/null && echo "nvim" || echo "vim")

if [[ $UNAME == "Linux" ]]; then
	export ZSH="/usr/share/oh-my-zsh-git"
	export UPDATE_ZSH_DAYS=7
	export ZSH_CACHE_DIR=$HOME/.cache/oh-my-zsh
	[[ ! -d $ZSH_CACHE_DIR ]] && mkdir $ZSH_CACHE_DIR
elif [[ $UNAME == "Darwin" ]]; then
	export ZSH="$HOME/.oh-my-zsh"
	zstyle ':omz:update' mode auto
	zstyle ':omz:update' frequency 7
fi

# https://github.com/ohmyzsh/ohmyzsh/wiki/Themes
ZSH_THEME="eastwood"
COMPLETION_WAITING_DOTS="true"
DISABLE_UNTRACKED_FILES_DIRTY="true"

plugins=(
	git
	fzf
		# (https://github.com/junegunn/fzf)
		# CTRL-T (search files from pwd)
		# CTRL-R (search bash history)
		# ALT-C (search dirs, cd into them)
)

source $ZSH/oh-my-zsh.sh

export PATH="$HOME/.local/bin:$PATH"

# CTRL-A (attach to tmux)
bindkey -s "^a" "tmux a\n"
# CTRL-F (attach to tmux session via tmux-sessioniser)
bindkey -s "^f" "$HOME/_dotfiles/tmux-sessioniser.sh\n"

alias cdx="cd ~/_dev"
alias cdz="cd ~/_zone"
alias cdd="cd ~/_dotfiles"

alias srczsh="source ~/.zshrc"
alias cfgzsh="(cd ~ && nvim .zshrc)"
alias cfggit="(cd ~ && nvim .gitconfig)"
alias cfgvim="(cd ~/.config/nvim && nvim .)"

alias ls="eza"
alias l="ls -l"
alias la="ls -la"

alias v="nvim"
alias vi="nvim"

alias lg="lazygit"

alias todo="$EDITOR ~/_zone/todo.txt"

# g_it f_uzzy clean
# Multi-select untracked files/folders to delete.
# usage: gfclean
function gfclean() {
	git clean -xdn | awk '{print$3}' | fzf -m | xargs rm -rf
}

# g_it f_uzzy check_out file
# Checkout a specific file from a specific branch.
# usage: gfcheckfile [<REF>]
# - REF: a branch name or commit
function gfcheckfile() {
	ref=${1:-$(git branch --format='%(refname:short)' | fzf)}
	git diff-tree -r --no-commit-id --name-only $ref | fzf -m | xargs git checkout --
}

# d_ocker f_uzzy clean c_ontainers
# Multi-select running containers to force remove.
# usage: dfcleanc
function dfcleanc() {
	docker ps | tail -n +2 | fzf -m | awk '{print$1}' | xargs docker rm -f
}

# d_ocker f_uzzy clean i_mages
# Multi-select images to force remove.
# usage: dfcleani
function dfcleani() {
	docker images | tail -n +2 | fzf -m | awk '{print$1}' | xargs docker rmi -f
}

# Loads environment variables from a given FILE into the current shell.
# usage: envload <FILE>
# - FILE: path to target file to load.
function envload() {
	export $(grep -v '^#' $1 | xargs -0)
}

# Kills any process that is using a given PORT.
# usage: deport <PORT> [<FILTER>]
# - PORT: numeric port to target (e.g. 3000)
# - FILTER: (optional) grep filter to target processes by name
function deport() {
	port=$1
	filter=$2
	# get processes using port, discard first line of output
	matches="$(lsof -i:$port | tail -n +2)"
	# optionally filter process names with grep
	if [[ -n "$filter" ]]; then
		matches="$(echo $matches | grep $filter)"
	fi
	if [[ -z "$matches" ]]; then
		echo no matches
		return
	fi
	pids="$(echo $matches | awk '{print$2}')"
	echo $pids | xargs kill
	echo $matches
}

if [[ $UNAME == "Linux" ]]; then
	source ~/_dotfiles/zsh/zshrc.linux
elif [[ $UNAME == "Darwin" ]]; then
	source ~/_dotfiles/zsh/zshrc.macos
fi
